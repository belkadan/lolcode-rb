HAI 1.3

O HAI IM pair
	I HAS A first
	I HAS A rest

	HOW IZ I finding YR item
		BOTH SAEM MAH first AN item
		O RLY?
			YA RLY, FOUND ME
			MEBBE MAH rest, MAH rest IZ finding item
			NO WAI, FAIL
		OIC
	IF U SAY SO

	HOW IZ I visible
		VISIBLE MAH first " "!
		MAH rest, O RLY?
			YA RLY, MAH rest IZ visible
			NO WAI, VISIBLE "", BTW newline
		OIC
	IF U SAY SO
KTHX

HOW IZ I consing YR left AN YR right
	I HAS A pear ITZ LIEK pair
	pear'Z first R left
	pear'Z rest R right
	FOUND YR pear
IF U SAY SO

HOW IZ I appending YR left AN YR right
	NOT left, O RLY?
		YA RLY, FOUND YR right
	OIC
	I IZ appending left'Z rest AN right
	I IZ consing left'Z first AN IT
IF U SAY SO

HOW IZ I listing ALL YR things
	I HAS A head
	IM IN YR things reversin YR elements
		I IZ consing IT AN head
		head R IT
	KTHX
IF U SAY SO

ME IZ frist, O RLY?
YA RLY
	CAN HAS "tests/test"
	
	O HAI IM A test
		MAH name R "listing"
		I IZ disabled, BTW because BOTH SAEM can't be changed right now...

		HOW IZ I testin
			ME IZ checkin BOTH SAEM I IZ listing MKAY AN NOOB AN "()"
			ME IZ checkin BOTH SAEM I IZ listing 1 MKAY AN I IZ consing 1 AN NOOB MKAY AN "(1)"
			ME IZ checkin BOTH SAEM...
				I IZ listing 1 AN 2 AN 3 MKAY AN...
				I IZ consing 1 AN I IZ consing 2 AN I IZ consing 3 AN NOOB MKAY MKAY MKAY AN...
				"(1 2 3)"
		IF U SAY SO

		I IZ runnin
	KTHX
OIC

KTHXBAI