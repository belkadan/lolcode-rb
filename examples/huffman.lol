HAI 1.3

CAN HAS "pairs"

O HAI IM leaf
	I HAS A symbol
	I HAS A weight

	HOW IZ I symbolizing
		I IZ consing MAH symbol AN NOOB
	IF U SAY SO

	HOW IZ I growing YR symbol AN YR weight
		I HAS A new ITZ LIEK leaf
		new'Z symbol R symbol
		new'Z weight R weight
		FOUND new
	IF U SAY SO
KTHX

O HAI IM branch
	I HAS A left
	I HAS A right
	I HAS A symbols
	I HAS A weight

	HOW IZ I symbolizing
		FOUND MAH symbols
	IF U SAY SO

	HOW IZ I joining YR left AN YR right
		I HAS A join ITZ LIEK branch
		join'Z left R left
		join'Z right R right
		join'Z weight R SUM OF left'Z weight AN right'Z weight

		I IZ appending left IZ symbolizing MKAY AN right IZ symbolizing MKAY
		join'Z symbols R IT

		FOUND YR join
	IF U SAY SO
KTHX

HOW IZ I decoding YR bits AN YR tree
	HOW IZ I choosing YR bit AN YR branch
		bit, O RLY?
			YA RLY, branch'Z right
			NO WAI, branch'Z left
		OIC
	IF U SAY SO

	I HAS A branch ITZ tree
	IM IN YR bits gettin YR items
		I IZ choosing IT AN branch
		branch R IT
		branch IZ liek leaf, O RLY?
		YA RLY
			VISIBLE branch'Z symbol!
			branch R tree
		OIC	
	KTHX
	VISIBLE "", BTW newline
IF U SAY SO

HOW IZ I encoding YR message AN YR tree
	HOW IZ I stepping YR symbol AN YR tree
		tree IZ symbolizing, IT IZ finding symbol
		NOT IT, O RLY?
		YA RLY
			tree IZ symbolizing, IT IZ visible
			DO NOT WANT SMOOSH "bad symbol (not in code tree): " AN symbol MKAY
		OIC

		tree IZ liek leaf, O RLY?
		YA RLY
			NOOB
		NO WAI
			tree'Z left IZ symbolizing, IT IZ finding symbol
			O RLY?
			YA RLY
				I IZ stepping symbol AN tree'Z left
				I IZ consing 0 AN IT
			NO WAI
				I IZ stepping symbol AN tree'Z right
				I IZ consing 1 AN IT
			OIC
		OIC
	IF U SAY SO

	message, O RLY?
	YA RLY
		I IZ encoding YR message'Z rest AN tree, I HAS A rest ITZ IT
		I IZ stepping YR message'Z first AN tree
		I IZ appending IT AN rest
	NO WAI
		NOOB
	OIC
IF U SAY SO


branch IZ joining...
	leaf IZ growing "E" AN 3 MKAY...
	AN branch IZ joining...
		leaf IZ growing "A" AN 2 MKAY...
		AN branch IZ joining...
			branch IZ joining...
				branch IZ joining...
					leaf IZ growing "G" AN 2 MKAY...
					AN leaf IZ growing "U" AN 1 MKAY...
				MKAY...
				AN leaf IZ growing "N" AN 1 MKAY...
			MKAY...
			AN branch IZ joining...
				branch IZ joining...
					leaf IZ growing "L" AN 1 MKAY...
					AN branch IZ joining...
						leaf IZ growing "M" AN 1 MKAY...
						AN leaf IZ growing "C" AN 1 MKAY...
					MKAY...
				MKAY...
				AN branch IZ joining...
					branch IZ joining...
						leaf IZ growing "H" AN 1 MKAY...
						AN leaf IZ growing "S" AN 1 MKAY...
					MKAY...
					AN leaf IZ growing " " AN 1 MKAY...
				MKAY...
			MKAY...
		MKAY...
	MKAY...
MKAY, I HAS A sample_tree ITZ IT

BTW yuck...LINE literals?
I IZ consing 1 AN...
I IZ consing 1 AN...
I IZ consing 1 AN...
I IZ consing 1 AN...
I IZ consing 0 AN...
I IZ consing 1 AN...
I IZ consing 1 AN...
I IZ consing 1 AN...
I IZ consing 1 AN...
I IZ consing 0 AN...
I IZ consing 1 AN...
I IZ consing 1 AN...
I IZ consing 1 AN...
I IZ consing 1 AN...
I IZ consing 1 AN...
I IZ consing 1 AN...
I IZ consing 0 AN...
I IZ consing 0 AN...
I IZ consing 0 AN...
I IZ consing 1 AN...
I IZ consing 1 AN...
I IZ consing 1 AN...
I IZ consing 0 AN...
I IZ consing 1 AN...
I IZ consing 0 AN...
I IZ consing 0 AN...
NOOB, I HAS A sample_message ITZ IT

I IZ decoding YR sample_message AN sample_tree

CAN HAS "../tests/line"
LINE IZ forming...
	1 AN 1 AN 1 AN 1 AN 0 AN...
	1 AN 1 AN 1 AN 1 AN 0 AN...
	1 AN 1 AN 1 AN 1 AN 1 AN...
	1 AN 0 AN 0 AN 0 AN 1 AN...
	1 AN 1 AN 0 AN 1 AN 0 AN...
	0
I HAS A line_message ITZ IT
I IZ decoding line_message AN sample_tree 

I IZ consing "S" AN...
I IZ consing "C" AN...
I IZ consing "H" AN...
I IZ consing "E" AN...
I IZ consing "M" AN...
I IZ consing "E" AN...
I IZ consing " " AN...
I IZ consing "L" AN...
I IZ consing "A" AN...
I IZ consing "N" AN...
I IZ consing "G" AN...
I IZ consing "U" AN...
I IZ consing "A" AN...
I IZ consing "G" AN...
I IZ consing "E" AN...
NOOB, I IZ encoding IT AN sample_tree
I IZ decoding IT AN sample_tree

KTHXBAI
