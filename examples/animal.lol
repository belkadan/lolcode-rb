HAI 1.3

O HAI IM guess
	I HAS A animal

	HOW IZ I new YR animal
		I HAS A new ITZ LIEK guess
		new'Z animal R animal
		FOUND YR new
	IF U SAY SO

	HOW IZ I guessin
		VISIBLE "Is it a " MAH animal "? " !
		I HAS A answer, GIMMEH answer
		BOTH SAEM answer AN NOOB, O RLY?
		YA RLY
			DO NOT WANT finished
		MEBBE NOT answer
			ME IZ guessin, BTW again
		MEBBE BOTH SAEM answer AN "yes"
			VISIBLE "I win!"
			FOUND ME
		NO WAI
			ME IZ learnin, FOUND IT
		OIC
	IF U SAY SO

	HOW IZ I learnin
		VISIBLE ":)I give up. What is it? " !
		I HAS A answer, GIMMEH answer
		I HAS A new ITZ guess IZ new YR answer MKAY
		
		VISIBLE "Enter a question where the answer is YES for a " answer...
			" and NO for a " MAH animal ": " !
		I HAS A sentence, GIMMEH sentence
		VISIBLE "Thanks!"

		question IZ new sentence AN new AN ME
	IF U SAY SO
KTHX

O HAI IM question
	I HAS A question
	I HAS A yes
	I HAS A no

	HOW IZ I new YR ask AN YR yes AN YR no
		I HAS A new ITZ LIEK question
		new'Z question R ask
		new'Z yes R yes
		new'Z no R no
		FOUND YR new
	IF U SAY SO

	HOW IZ I guessin
		VISIBLE MAH question " " !
		I HAS A answer, GIMMEH answer
		BOTH SAEM answer AN NOOB, O RLY?
		YA RLY
			DO NOT WANT finished
		MEBBE NOT answer
			ME IZ guessin, BTW again
		MEBBE BOTH SAEM answer AN "yes"
			MAH yes IZ guessin, MAH yes R IT
		NO WAI
			MAH no IZ guessin, MAH no R IT
		OIC
		FOUND ME
	IF U SAY SO
KTHX

HOW IZ I playing YR animals
	VISIBLE "Think of an animal!"
	animals IZ guessin
IF U SAY SO

question IZ new "Does it have wings?"...
	AN guess IZ new "parrot" MKAY...
	AN guess IZ new "rabbit" MKAY
I HAS A animals ITZ IT

BTW baby exception
ME HAS A finished ITZ LIEK BUKKIT

ME IZ frist, O RLY?
YA RLY
	PLZ
		IM IN YR animals, BTW forever
			I IZ playing YR animals
			VISIBLE ""
		KTHX
	O NOES ITZ A finished
		VISIBLE ":):)Thanks for playing!"
	KTHX
OIC

KTHXBAI