HAI 1.3

CAN HAS "test"

BTW FIXME better path mechanism
CAN HAS "../simple/line"

O HAI IM A test
	MAH name R "NOOB"

	HOW IZ I testin
		LINE IZ forming NOOB AN 0 AN 0.0 AN "" AN FAIL AN 3 AN 3.0 AN "3.0" AN WIN
		I HAS A inputs ITZ IT
		IM IN YR inputs gettin YR things
			ME IZ checkin BOTH SAEM NOOB AN MAEK IT A NOOB...
				AN SMOOSH "MAEK " AN IT AN " A NOOB" MKAY
		KTHX
	IF U SAY SO

	I IZ runnin
KTHX

O HAI IM A test
	MAH name R "TROOF"

	HOW IZ I testin
		LINE IZ forming NOOB AN 0 AN 0.0 AN "" AN FAIL
		I HAS A fails ITZ IT
		IM IN YR fails gettin YR things
			ME IZ checkin BOTH SAEM FAIL AN MAEK IT A TROOF...
				AN SMOOSH "MAEK " AN IT AN " A TROOF" MKAY
		KTHX

		LINE IZ forming 3 AN 3.0 AN "3.0" AN WIN
		I HAS A wins ITZ IT
		IM IN YR wins gettin YR things
			ME IZ checkin BOTH SAEM WIN AN MAEK IT A TROOF...
				AN SMOOSH "MAEK " AN IT AN " A TROOF" MKAY
		KTHX
	IF U SAY SO

	I IZ runnin
KTHX

O HAI IM A test
	MAH name R "NUMBR"

	HOW IZ I testin
		LINE IZ forming NOOB AN 0 AN 0.0 AN "" AN FAIL
		I HAS A zeroes ITZ IT
		IM IN YR zeroes gettin YR things
			ME IZ checkin BOTH SAEM 0 AN MAEK IT A NUMBR...
				AN SMOOSH "MAEK " AN IT AN " A NUMBR" MKAY
		KTHX

		LINE IZ forming 3 AN 3.0 AN "3.0"
		I HAS A threes ITZ IT
		IM IN YR threes gettin YR things
			ME IZ checkin BOTH SAEM 3 AN MAEK IT A NUMBR...
				AN SMOOSH "MAEK " AN IT AN " A NUMBR" MKAY
		KTHX

		ME IZ checkin BOTH SAEM 1 AN MAEK WIN A NUMBR AN "MAEK WIN A NUMBR"
	IF U SAY SO

	I IZ runnin
KTHX

O HAI IM A test
	MAH name R "NUMBAR"

	HOW IZ I testin
		LINE IZ forming NOOB AN 0 AN 0.0 AN "" AN FAIL
		I HAS A zeroes ITZ IT
		IM IN YR zeroes gettin YR things
			ME IZ checkin BOTH SAEM 0.0 AN MAEK IT A NUMBAR...
				AN SMOOSH "MAEK " AN IT AN " A NUMBAR" MKAY
		KTHX

		LINE IZ forming 3 AN 3.0 AN "3.0"
		I HAS A threes ITZ IT
		IM IN YR threes gettin YR things
			ME IZ checkin BOTH SAEM 3.0 AN MAEK IT A NUMBAR...
				AN SMOOSH "MAEK " AN IT AN " A NUMBAR" MKAY
		KTHX

		ME IZ checkin BOTH SAEM 1.0 AN MAEK WIN A NUMBAR AN "MAEK WIN A NUMBAR"
	IF U SAY SO

	I IZ runnin
KTHX

O HAI IM A test
	MAH name R "YARN"

	HOW IZ I testin
		ME IZ checkin BOTH SAEM "NOOB" AN MAEK NOOB  A YARN AN "MAEK NOOB A YARN"
		ME IZ checkin BOTH SAEM "0"    AN MAEK 0     A YARN AN "MAEK 0 A YARN"
		ME IZ checkin BOTH SAEM "0.0"  AN MAEK 0.0   A YARN AN "MAEK 0.0 A YARN"
		ME IZ checkin BOTH SAEM ""     AN MAEK ""    A YARN AN "MAEK :":" A YARN"
		ME IZ checkin BOTH SAEM "FAIL" AN MAEK FAIL  A YARN AN "MAEK FAIL A YARN"
		ME IZ checkin BOTH SAEM "3"    AN MAEK 3     A YARN AN "MAEK 3 A YARN"
		ME IZ checkin BOTH SAEM "3.0"  AN MAEK 3.0   A YARN AN "MAEK 3.0 A YARN"
		ME IZ checkin BOTH SAEM "3.0"  AN MAEK "3.0" A YARN AN "MAEK :"3.0:" A YARN"
		ME IZ checkin BOTH SAEM "WIN"  AN MAEK WIN   A YARN AN "MAEK WIN A YARN"
	IF U SAY SO

	I IZ runnin
KTHX

KTHXBYE
